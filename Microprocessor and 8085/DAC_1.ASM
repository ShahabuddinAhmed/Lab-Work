;*****************************************
;     MDA-Win8086 EXPERIMENT PROGRAM    *
;     FILENAME  : DAC_1.ASM
;     PROCESSOR : I8086
;     DAC
;*****************************************
CODE	SEGMENT
	ASSUME	CS:CODE,DS:CODE,ES:CODE,SS:CODE
	;
PPIC_C	EQU	1FH
PPIC	EQU	1DH
PPIB	EQU	1BH
PPIA	EQU	19H
	;	
AD_C	EQU	18H
	;
LCDC	EQU	00H
LCDC_S	EQU	02H
LCDD	EQU	04H
	;	
	ORG	1000H
	MOV	AL,10000000B
	OUT	PPIC_C,AL
	;
	MOV	AL,11111111B
	OUT	PPIA,AL
	MOV	AL,11110000B
	OUT	PPIB,AL
	; DA INITIAL VALUE
	MOV	AL,00000000B
	OUT	PPIC,AL
	;
	CALL	ALLCLR
	MOV	SI,OFFSET DATA
	CALL	STRING
	;
L2:	MOV	BL,0
L1:	CALL	DISP_AD
	MOV	AL,BL
	OUT	PPIC,AL
	;
	OUT	AD_C,AL
	;
	CALL	TIMER
	;
	IN	AL,AD_C
	;
	CALL	CON_VOLT
	;
	CALL	DISP_V
	INC	BL
	JNZ	L1
	JMP	L2
	;
	; 
DISP_AD: PUSH	BX
	CALL	LN21
	;
	MOV	AH,0C0H+3
	CALL	LNXX
	; 100
	MOV	AL,BL	
	MOV	AH,0
BCD1:	SUB	AL,100
	INC	AH
	JNC	BCD1
	DEC	AH
	ADD	AL,100
	;
	PUSH	AX
	CALL	H_A1
	POP	AX
	; 10
	MOV	AH,0
BCD2:	SUB	AL,10
	INC	AH
	JNC	BCD2
	DEC	AH
	ADD	AL,10
	;
	PUSH	AX
	CALL	H_A1
	POP	AX
	; 1
	MOV	AH,AL
	CALL	H_A1
	;
	POP	BX
	RET
	;
	RET
	; INPUT = DX
DISP_V:	MOV	AH,0C0H+9
	CALL	LNXX
	;
	MOV	AH,DH
	AND	AH,0F0H
	MOV	CL,4
	SHR	AH,CL
	CALL	H_A1
	;
	MOV	AH,'.'
	CALL	CHAROUT
	;
	MOV	AH,DH
	AND	AH,0FH
	CALL	H_A1
	;
	MOV	AH,DL
	CALL	H_A
	;
	MOV	SI,OFFSET DATA1
	CALL	STRING
	;
	RET
	;
DATA1:	DB	'V',00H
	;
	; IN =AL OUT =DX			
CON_VOLT:
	PUSH	AX
	AND	AX,00F0H
	MOV	DI,OFFSET H_TAB
	;
	MOV	CL,4
	SHR	AL,CL
	;
	ADD	AL,AL
	ADC	AH,0
	ADD	DI,AX
	MOV	DH,BYTE PTR CS:[DI]
	INC	DI
	MOV	DL,BYTE PTR CS:[DI]
	;
	POP	AX
	AND	AX,000FH
	MOV	DI,OFFSET L_TAB
	;
	ADD	AL,AL
	ADC	AH,0
	ADD	DI,AX
	MOV	AH,BYTE PTR CS:[DI]
	INC	DI
	MOV	AL,BYTE PTR CS:[DI]
	;
	ADD	AL,DL
	DAA
	MOV	DL,AL
	;
	MOV	AL,AH
	ADD	AL,DH
	DAA
	MOV	DH,AL
	RET
	;
L_TAB:	DB	00H,00H
	DB	0,20H
	DB	0,40H
	DB	0,60H
	DB	0,80H
	DB	01H,00H
	DB	01H,20H
	DB	01H,40H
	DB	01H,60H
	DB	01H,80H
	DB	02H,00H
	DB	02H,20H
	DB	02H,40H
	DB	02H,60H
	DB	02H,80H
	DB	03H,00H
	;
H_TAB:	DB	0,0
	DB	03H,20H ;0.320V
	DB	06H,40H
	DB	09H,60H
	DB	12H,80H
	DB	16H,00H
	DB	19H,20H
	DB	22H,40H
	DB	25H,60H
	DB	28H,80H
	DB	32H,00H
	DB	35H,20H
	DB	38H,40H
	DB	41H,60H
	DB	44H,80H
	DB	48H,00H  ; 4.8V
	;
DATA:	DB	'    DA     AD',00H
	;		
	;		
	; LCD instruction
ALLCLR:	MOV	AH,01H
	JMP	LNXX
	;
LN21:	MOV	AH,0C0H
LNXX:	CALL	BUSY
	MOV	AL,AH
	OUT	LCDC,AL
	RET
	; busy flag check
BUSY:	IN	AL,LCDC_S
	AND	AL,10000000B
	JNZ	BUSY
	RET
	;
	; 1 char. LCD OUT 
	; AH = out data
CHAROUT:
	CALL	BUSY
	;
	MOV	AL,AH
	OUT	LCDD,AL
	RET
	;
STRING:	MOV	AH,BYTE PTR CS:[SI]
	CMP	AH,00H
	JE	STRING1
	; out
	CALL	BUSY
	CALL	CHAROUT
	INC	SI
	JMP	STRING
STRING1:
  	RET
	;		
	; 1 byte data LCD out
	; INPUT DATA AH
H_A     PROC	NEAR
	PUSH	AX
	ROR	AH,1
	ROR	AH,1
	ROR	AH,1
	ROR	AH,1
	CALL	H_A1 ; high digit lcd out
	POP	AX
	CALL	H_A1 ; low digit lcd out
	RET
H_A	ENDP
	;				
H_A1    PROC	NEAR
	PUSH	BX
	MOV	BX,OFFSET ASCTBL
	AND	AH,0FH
	ADD	BL,AH
	JNC	H_A2
	INC	BH
H_A2:	MOV 	AH,CS:[BX]
	CALL	CHAROUT
	POP	BX
	RET
	;
ASCTBL	DB	'0123456789'
H_A1	ENDP

TIMER:	MOV	CX,1
TIMER2:	PUSH	CX
       	MOV	CX,0
TIMER1:	NOP
	LOOP	TIMER1
	POP	CX
	LOOP	TIMER2
	RET
	;
CODE	ENDS
	END


